!*** If compilation and attachment of the user defined element was successful 
!*** copy this code to ansys mechanical comand line for testing 
FINISH
/clear
!*** Define parameters related to model

E  = 210000     !- Young's modulus [MPa]
v  = 0.3	      !- Poisson's ratio
S02= 300        !- Yield Stress [Mpa]
Stg= 10000      !- Tangential modulus
L  = 0.1	      !- Phase Field length parameter [mm]
Gc = 2.7	      !- Critical energy release rate [MPa mm]
C1 = 0          !- If C1 nonzero then Gc = C1*temp^C2
C2 = 0
C3 = 0

DisipH = 0.1    !- Energy dissipation (1 - no dissipation, 0 - all plastic work dissipated as heat) 
BW_C1  = 0 		  !- Coefficients of multiaxial function based on Bai-Wierzbicki model
BW_C2  = 0 

!*** Define plate size
PLTWDTH = 1     
PLTHGT  = 1 
CRCKL   = 0.5
NITER=100

/PREP7
!*** Add userelement 
ET,1,USER300
!*USRELEM, NNODES, NDIM, KeyShape, NREAL, NSAVEVARS, NRSLTVAR, KEYANSMAT, NINTPNTS, KESTRESS, KEYSYM
USRELEM, 4, 2, QUAD, 9, 100, 100, 1, 4, 2, 0
R,1,Gc,L,C1,C2,C3,0,
RMORE,BW_C1,BW_C2,DisipH
usrdof,Define,ux,uy,uz

!*** Elastic properties
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,E 
MPDATA,PRXY,1,,v  

!*** Plastic properties
TB,BISO,1,1,2,  
TBDATA,,S02,Stg,,,,

!***************************CREATE GEOMETRY

!*** KEYPOINTS
K,1,CRCKL,PLTHGT/2,,
K,2,,PLTHGT/2,,
K,3,,,,
K,4,CRCKL,,,
K,5,PLTWDTH,
K,6,PLTWDTH,PLTHGT/2,,
K,7,PLTWDTH,PLTHGT,,
K,8,CRCKL,PLTHGT,,
K,9,,PLTHGT,,
K,10,,PLTHGT/2,,

!*** LINES
LSTR,       1,       2 
LSTR,       2,       3  
LSTR,       3,       4 
LSTR,       4,       5 
LSTR,       5,       6 
LSTR,       6,       7  
LSTR,       7,       8
LSTR,       8,       9
LSTR,       9,       10
LSTR,       1,       10
LSTR,       1,       4
LSTR,       1,       6
LSTR,       1,       8

!*** AREAS
AL,1,2,3,11 
AL,4,5,12,11 
AL,6,7,13,12 
AL,8,9,10,13 

!*************************** MESHING
SMRT,1  
MSHAPE,0,2D 
MSHKEY,0
AMESH,ALL  
!*** Uncomment next lines to refine FE model
EREF,ALL, , ,1,0,1,1
!EREF,ALL, , ,1,0,1,1
!EREF,ALL, , ,1,0,1,1
!*************************** LOADS

DL,3, ,UY,0  
DL,4, ,UY,0  

DK,5, , , ,0,UX, , , , , ,   

DL,7, ,UY,0.1 
DL,8, ,UY,0.1  

!*************************** SOLUTION
FINISH
/SOL
ANTYPE,0
OUTRES,ALL,ALL
OUTRES,SVAR,ALL
auto,1
time,1
nsubst,NITER,NITER*10000,NITER/10
SAVE
SOLVE

!*************************** RESULTS
FINISH 
/POST1
SET,LAST
PLNSOL, U,Z, 0,1.0  
/REPLOT 
